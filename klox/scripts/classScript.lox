
print "Basic class, no properties, method calls, or instantiation.";

class DevonshireCream {
    serveOn() {
        return "Scones";
    }
}

print DevonshireCream;

print "";
print "Class instantiation.";

class Bagel {
}

var bagel = Bagel();
print bagel;

print "";
print "Method calls.";

class Bacon {
    eat() {
        print "Crunch, crunch, crunch";
    }
}

var bacon = Bacon();
bacon.eat();

print "";
print "Method closures.";

class Egoist {
    speak() {
        print this;
    }
}

var egoist = Egoist();
egoist.speak();

var method = egoist.speak;
method();

print "";
print "Properties and closures.";

class Cake {
    taste() {
        var adjective = "delicious";
        print "The " + this.flavor + " cake is " + adjective + "!";
    }
}

var cake = Cake();
cake.flavor = "German chocolate";
cake.taste();

print "";
print "Callbacks.";

class Thing {
    getCallback() {
        fun local() {
            print this;
        }
        return local;
    }
}

var callback = Thing().getCallback();
callback();

print "";
print "initializer";

class Foo {
    init(name) {
        this.name = name;
        print this;
    }

    greet() {
        print "Greetings, " + this.name;
    }
}

Foo("Zaphod").greet();

print Foo("Arthur").init("something");


print "";
print "inheritance";

class Doughnut {
    cook() {
        print "Fry until golden";
    }
}

class BostonCreme < Doughnut {}

BostonCreme().cook();
